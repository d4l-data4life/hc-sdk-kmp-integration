apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "de.gesundheitscloud.sdk.integration"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: "true"

        manifestPlaceholders = [
                clientId      : "android",
                clientSecret  : "androidsupersecret",
                environment   : "development",
                redirectScheme: "de.gesundheitscloud.example",
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "environment"

    productFlavors {
        development {
            dimension "environment"
            manifestPlaceholders = [
                    environment: "development",
            ]
        }
        production {
            dimension "environment"
            manifestPlaceholders = [
                    environment: "production",
            ]
        }
    }

    defaultPublishConfig "stagingDebug"


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main.res.srcDirs = [
                'src/main/res',

                // feature resources
                'src/main/res_feature/home',
                'src/main/res_feature/document',
                'src/main/res_feature/welcome',
        ]

    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.0.0'

    def androidXVersion = "1.0.0"
    implementation "androidx.appcompat:appcompat:$androidXVersion"
    implementation "androidx.browser:browser:$androidXVersion"

    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha2"


    def lifecycleVersion = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    def navigationVersion = "1.0.0-alpha06"
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigationVersion"

    // http://developer.android.com/tools/extras/support-library.html
    def materialVersion = "1.0.0"
    implementation "com.google.android.material:material:$materialVersion"

//    implementation 'de.gesundheitscloud:hc-sdk-android:1.0.0-rc.2'
    implementation('de.gesundheitscloud.hc-sdk-android:sdk-android:ef50a7859d') {
        exclude group: 'de.gesundheitscloud.hc-sdk-android', module: 'securestore-jvm'
        exclude group: 'de.gesundheitscloud.hc-sdk-android', module: 'crypto-jvm'
        exclude group: 'de.gesundheitscloud.hc-sdk-android', module: 'auth-jvm'
        exclude group: 'de.gesundheitscloud.hc-sdk-android', module: 'util-jvm'
    }


    testImplementation 'junit:junit:4.12'


    def androidTestVersion = "1.1.0-alpha4"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestUtil "androidx.test:orchestrator:$androidTestVersion"

    def espressoVersion = "3.1.0-alpha4"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"

    def uiautomatorVersion = "2.2.0-alpha4"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$uiautomatorVersion"

    androidTestImplementation "android.arch.navigation:navigation-testing:$navigationVersion"

    // https://github.com/agoda-com/Kakao
    // androidTestImplementation 'com.agoda.kakao:kakao:1.4.0'
    // currently patched version for AndroidX from https://github.com/wmontwe/Kakao
    androidTestImplementation 'com.github.wmontwe:Kakao:1.4.0-androidx'
}
